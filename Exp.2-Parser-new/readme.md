# Grammar

```bison
Program ->
    GlobalDeclDefList

GlobalDeclDefList ->
    GlobalDeclDefList GlobalDeclDef
    | GlobalDeclDef

GlobalDeclDef ->
    Type VarList PUNCTUATOR_SEMICOLON
    | Type Assignable PUNCTUATOR_PARENTHESIS_LEFT ParamList PUNCTUATOR_PARENTHESIS_RIGHT BlockStmt
    | Type Assignable PUNCTUATOR_PARENTHESIS_LEFT ParamList PUNCTUATOR_PARENTHESIS_RIGHT PUNCTUATOR_SEMICOLON
    | [error] PUNCTUATOR_SEMICOLON

Type ->
    KEYWORD_TYPE
    | StructType

Assignable ->
    Identifier
    | IndexExpr

Identifier ->
    IDENTIFIER

VarList ->
    Assignable
    | AssignmentExpr
    | VarList PUNCTUATOR_COMMA Assignable
    | VarList PUNCTUATOR_COMMA AssignmentExpr

AssignmentExpr ->
    Assignable OPERATOR_ASSIGNMENT Expr
    | Assignable OPERATOR_ASSIGNMENT ArrayLiteral

ParamList ->
    ParamList PUNCTUATOR_COMMA Param
    | Param
    | [eps]

Param ->
    Type Assignable

BlockStmt ->
    PUNCTUATOR_BRACE_LEFT StmtList PUNCTUATOR_BRACE_RIGHT
    | PUNCTUATOR_BRACE_LEFT PUNCTUATOR_BRACE_RIGHT
    | [error] PUNCTUATOR_BRACE_RIGHT

StmtList ->
    StmtList Stmt
    | Stmt

Stmt ->
    LocalDeclDef
    | BlockStmt
    | Expr PUNCTUATOR_SEMICOLON
    | KEYWORD_RETURN Expr PUNCTUATOR_SEMICOLON
    | KEYWORD_CONTINUE Expr PUNCTUATOR_SEMICOLON
    | KEYWORD_BREAK Expr PUNCTUATOR_SEMICOLON
    | KEYWORD_RETURN PUNCTUATOR_SEMICOLON
    | KEYWORD_CONTINUE PUNCTUATOR_SEMICOLON
    | KEYWORD_BREAK PUNCTUATOR_SEMICOLON
    | KEYWORD_IF PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_PARENTHESIS_RIGHT Stmt %prec IF_END
    | KEYWORD_IF PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_PARENTHESIS_RIGHT Stmt KEYWORD_ELSE Stmt
    | KEYWORD_WHILE PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_PARENTHESIS_RIGHT Stmt
    | KEYWORD_DO Stmt KEYWORD_WHILE PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_PARENTHESIS_RIGHT PUNCTUATOR_SEMICOLON
    | KEYWORD_FOR PUNCTUATOR_PARENTHESIS_LEFT LocalDeclDef Expr PUNCTUATOR_SEMICOLON Expr PUNCTUATOR_PARENTHESIS_RIGHT Stmt
    | KEYWORD_FOR PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_SEMICOLON Expr PUNCTUATOR_SEMICOLON Expr PUNCTUATOR_PARENTHESIS_RIGHT Stmt
    | KEYWORD_SWITCH PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_PARENTHESIS_RIGHT PUNCTUATOR_BRACE_LEFT SwitchBodyStmt PUNCTUATOR_BRACE_RIGHT
    | [error] PUNCTUATOR_SEMICOLON

SwitchBodyStmt ->
    CaseStmtList DefaultStmt
    | CaseStmtList
    | DefaultStmt
    | [eps]

CaseStmtList ->
    CaseStmtList CaseStmt
    | CaseStmt

CaseStmt ->
    KEYWORD_CASE Expr PUNCTUATOR_COLON StmtList
    | KEYWORD_CASE Expr PUNCTUATOR_COLON

DefaultStmt ->
    KEYWORD_DEFAULT PUNCTUATOR_COLON StmtList
    | KEYWORD_DEFAULT PUNCTUATOR_COLON

LocalDeclDef ->
    Type VarList PUNCTUATOR_SEMICOLON

Expr ->
    Assignable
    | Literal
    | CallExpr
    | AssignmentExpr
    | Assignable OPERATOR_COMPOUND_ASSIGNMENT Expr
    | Expr OPERATOR_QUESTION Expr PUNCTUATOR_COLON Expr
    | Expr OPERATOR_LOGICAL_AND Expr
    | Expr OPERATOR_LOGICAL_OR Expr
    | Expr OPERATOR_BITWISE_AND Expr
    | Expr OPERATOR_BITWISE_OR Expr
    | Expr OPERATOR_BITWISE_XOR Expr
    | Expr OPERATOR_SHIFT_RIGHT Expr
    | Expr OPERATOR_SHIFT_LEFT Expr
    | Expr OPERATOR_RELATIONAL Expr
    | Expr OPERATOR_PLUS Expr
    | Expr OPERATOR_MINUS Expr
    | Expr OPERATOR_MULTIPLY Expr
    | Expr OPERATOR_DIVIDE Expr
    | Expr OPERATOR_MODULUS Expr
    | PUNCTUATOR_PARENTHESIS_LEFT Expr PUNCTUATOR_PARENTHESIS_RIGHT
    | OPERATOR_PLUS Expr %prec OPERATOR_UNARY_PLUS
    | OPERATOR_MINUS Expr %prec OPERATOR_UNARY_MINUS
    | OPERATOR_LOGICAL_NOT Expr
    | OPERATOR_BITWISE_NOT Expr
    | OPERATOR_INCREMENT Expr
    | OPERATOR_DECREMENT Expr
    | Expr OPERATOR_INCREMENT
    | Expr OPERATOR_DECREMENT

Literal ->
    LITERAL_INTEGER
    | LITERAL_FLOAT
    | LITERAL_CHAR
    | LITERAL_STRING

ArrayLiteral ->
    PUNCTUATOR_BRACE_LEFT ArrayItemList PUNCTUATOR_BRACE_RIGHT

ArrayItemList ->
    ArrayItemList PUNCTUATOR_COMMA ArrayItem
    | ArrayItem
    | [eps]

ArrayItem ->
    Expr
    | ArrayLiteral

CallExpr ->
    CallExpr PUNCTUATOR_PARENTHESIS_LEFT ArgumentList PUNCTUATOR_PARENTHESIS_RIGHT
    | Assignable PUNCTUATOR_PARENTHESIS_LEFT ArgumentList PUNCTUATOR_PARENTHESIS_RIGHT

ArgumentList ->
    ArgumentList PUNCTUATOR_COMMA Expr
    | Expr
    | [eps]

IndexExpr ->
    IndexExpr PUNCTUATOR_BRACKET_LEFT Index PUNCTUATOR_BRACKET_RIGHT
    | Identifier PUNCTUATOR_BRACKET_LEFT Index PUNCTUATOR_BRACKET_RIGHT

Index ->
    Expr
    | [eps]
```
